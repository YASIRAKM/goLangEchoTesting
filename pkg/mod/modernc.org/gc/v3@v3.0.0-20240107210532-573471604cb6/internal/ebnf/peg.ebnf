AdditiveExpression         = MultiplicativeExpression         {  ( "+" | "-" | "|" | "^" ) MultiplicativeExpression         } .
AdditiveExpressionPreBlock = MultiplicativeExpressionPreBlock {  ( "+" | "-" | "|" | "^" ) MultiplicativeExpressionPreBlock } .
AliasDecl = identifier "=" Type .
Arguments = "(" [ ( ExpressionList | Type [ "," ExpressionList ] ) [ "..." ] [ "," ] ] ")" .
ArrayLength = Expression .
ArrayType = "[" ArrayLength "]" ElementType .
Assignment         = /* ExpressionList */         ( "=" | "+=" | "-=" | "|=" | "^=" | "*=" | "/=" | "%=" | "<<=" | ">>=" | "&=" | "&^=" ) ExpressionList .
AssignmentPreBlock = /* ExpressionListPreBlock */ ( "=" | "+=" | "-=" | "|=" | "^=" | "*=" | "/=" | "%=" | "<<=" | ">>=" | "&=" | "&^=" ) ExpressionListPreBlock .
BaseType = Type .
BasicLit = int_lit | float_lit | imaginary_lit | rune_lit | string_lit .
Block = "{" StatementList "}" .
BreakStmt = "break" [ Label ] .
Channel = Expression .
ChannelType = ( "chan" "<-" | "chan" | "<-" "chan" ) ElementType .
CommCase = "case" ( SendStmt | RecvStmt ) | "default" .
CommClause = CommCase ":" StatementList .
CompositeLitPreBlock = LiteralTypePreBlock LiteralValue .
Condition         = Expression .
ConditionPreBlock = ExpressionPreBlock .
ConstDecl = "const" ( ConstSpec | "(" { ConstSpec ";" } [ ConstSpec ] ")" ) .
ConstSpec = IdentifierList [ [ Type ] "=" ExpressionList ] .
ContinueStmt = "continue" [ Label ] .
Conversion = Type "(" Expression [ "," ] ")" .
Declaration = ConstDecl | TypeDecl | VarDecl .
DeferStmt = "defer" Expression .
Element = Expression | LiteralValue .
ElementList = KeyedElement { "," KeyedElement } .
ElementType = Type .
EmbeddedField = [ "*" ] TypeName [ TypeArgs ] .
EmptyStmt =  .
ExprCaseClause = ExprSwitchCase ":" StatementList .
ExprSwitchCase = "case" ExpressionList | "default" .
ExprSwitchStmt = "switch" [ ExpressionPreBlock ] "{" { ExprCaseClause } "}" | "switch" SimpleStmt ";" [ ExpressionPreBlock ] "{" { ExprCaseClause } "}" .
Expression         = LogicalAndExpression         { "||" LogicalAndExpression         } .
ExpressionPreBlock = LogicalAndExpressionPreBlock { "||" LogicalAndExpressionPreBlock } .
ExpressionList         = Expression { "," Expression } .
ExpressionListPreBlock = ExpressionPreBlock { "," ExpressionPreBlock } .
FallthroughStmt = "fallthrough" .
FieldDecl = ( IdentifierList Type | EmbeddedField ) [ Tag ] .
ForClause = [ InitStmt ] ";" [ Condition ] ";" [ PostStmt ] .
ForStmt = "for" [ ForClause | RangeClause | ConditionPreBlock ] Block .
FunctionBody = Block .
FunctionDecl = "func" FunctionName [ TypeParameters ] Signature [ FunctionBody ] .
FunctionLit = "func" Signature FunctionBody .
FunctionName = identifier .
FunctionType = "func" Signature .
GoStmt = "go" Expression .
GotoStmt = "goto" Label .
IdentifierList = identifier { "," identifier } .
IfStmt = "if" ExpressionPreBlock Block [ "else" ( IfStmt | Block ) ] | "if" SimpleStmt ";" ExpressionPreBlock Block [ "else" ( IfStmt | Block ) ] .
ImportDecl = "import" ( ImportSpec | "(" { ImportSpec ";" } [ ImportSpec ] ")" ) .
ImportPath = string_lit .
ImportSpec = [ "." | PackageName ] ImportPath .
Index = "[" Expression "]" .
InitStmt = SimpleStmtPreBlock .
InterfaceElem = MethodElem | TypeElem .
InterfaceType = "interface" "{" { InterfaceElem ";" } [ InterfaceElem ] "}" .
KeyType = Type .
KeyedElement = Element [ ":" Element ] .
Label = identifier .
LabeledStmt = Label ":" Statement .
Literal             = BasicLit | CompositeLitPreBlock | FunctionLit .
LiteralTypePreBlock = StructType | ArrayType | "[" "..." "]" ElementType | SliceType | MapType .
LiteralValue = "{" [ ElementList [ "," ] ] "}" .
LogicalAndExpression         = RelationalExpression         { "&&" RelationalExpression         } .
LogicalAndExpressionPreBlock = RelationalExpressionPreBlock { "&&" RelationalExpressionPreBlock } .
MapType = "map" "[" KeyType "]" ElementType .
MethodDecl = "func" Receiver MethodName Signature [ FunctionBody ] .
MethodElem = MethodName Signature .
MethodExpr = ReceiverType "." MethodName .
MethodName = identifier .
MultiplicativeExpression         = UnaryExpr         { ( "*" | "/" | "%" | "<<" | ">>" | "&" | "&^" ) UnaryExpr         } .
MultiplicativeExpressionPreBlock = UnaryExprPreBlock { ( "*" | "/" | "%" | "<<" | ">>" | "&" | "&^" ) UnaryExprPreBlock } .
Operand         = Literal | OperandName [ TypeArgs ] [ LiteralValue ] | "(" Expression ")" .
OperandPreBlock = Literal | OperandName [ TypeArgs ]                  | "(" Expression ")" .
OperandName = QualifiedIdent  | identifier .
PackageClause = "package" PackageName .
PackageName = identifier .
ParameterDecl = identifier "..." Type | identifier Type | "..." Type | Type .
ParameterList = ParameterDecl { "," ParameterDecl } .
Parameters = "(" [ ParameterList [ "," ] ] ")" .
PointerType = "*" BaseType .
PostStmt = SimpleStmtPreBlock .
PrimaryExpr         = ( Operand         | Conversion | MethodExpr ) { Selector | Index | Slice | TypeAssertion | Arguments } .
PrimaryExprPreBlock = ( OperandPreBlock | Conversion | MethodExpr ) { Selector | Index | Slice | TypeAssertion | Arguments } .
QualifiedIdent = PackageName "." identifier .
RangeClause = "range" ExpressionPreBlock | ExpressionList "=" "range" ExpressionPreBlock | IdentifierList ":=" "range" ExpressionPreBlock .
Receiver = Parameters .
ReceiverType = Type .
RecvExpr = Expression .
RecvStmt = [ ExpressionList "=" | IdentifierList ":=" ] RecvExpr .
RelationalExpression         = AdditiveExpression         { ( "==" | "!=" | "<" | "<=" | ">" | ">=" ) AdditiveExpression         } .
RelationalExpressionPreBlock = AdditiveExpressionPreBlock { ( "==" | "!=" | "<" | "<=" | ">" | ">=" ) AdditiveExpressionPreBlock } .
Result = Parameters | Type .
ReturnStmt = "return" [ ExpressionList ] .
SelectStmt = "select" "{" { CommClause } "}" .
Selector = "." identifier .
SendStmt = Channel "<-" Expression .
ShortVarDecl         = /* IdentifierList */ ":=" ExpressionList .
ShortVarDeclPreBlock = /* IdentifierList */ ":=" ExpressionListPreBlock .
Signature = Parameters [ Result ] .
SimpleStmt         = ExpressionList         [ Assignment         | ShortVarDecl         | "<-" Expression             | "++" | "--" ] | EmptyStmt .
SimpleStmtPreBlock = ExpressionListPreBlock [ AssignmentPreBlock | ShortVarDeclPreBlock | "<-" ExpressionListPreBlock | "++" | "--" ] | EmptyStmt .
Slice = "[" [ Expression ] ":" [ Expression ] "]" | "[" [ Expression ] ":" Expression ":" Expression "]" .
SliceType = "[" "]" ElementType .
SourceFile = PackageClause ";" { ImportDecl ";" } { TopLevelDecl ";" } .
Statement = Declaration | LabeledStmt | GoStmt | ReturnStmt | BreakStmt | ContinueStmt | GotoStmt | FallthroughStmt | Block | IfStmt | SwitchStmt | SelectStmt | ForStmt | DeferStmt | SimpleStmt .
StatementList = Statement { ";" Statement } [ ";" ] .
StructType = "struct" "{" { FieldDecl ";" } [ FieldDecl ] "}" .
SwitchStmt = ExprSwitchStmt | TypeSwitchStmt .
Tag = string_lit .
TopLevelDecl = Declaration | FunctionDecl | MethodDecl .
Type = TypeName [ TypeArgs ] | TypeLit | "(" Type ")" .
TypeArgs = "[" TypeList [ "," ] "]" .
TypeAssertion = "." "(" Type ")" .
TypeCaseClause = TypeSwitchCase ":" StatementList .
TypeConstraint = TypeElem .
TypeDecl = "type" ( TypeSpec | "(" { TypeSpec ";" } ")" ) .
TypeDef = identifier [ TypeParameters ] Type .
TypeElem = TypeTerm { "|" TypeTerm } .
TypeList = Type { "," Type } .
TypeLit = ArrayType | StructType | PointerType | FunctionType | InterfaceType | SliceType | MapType | ChannelType .
TypeName = QualifiedIdent | identifier .
TypeParamDecl = IdentifierList TypeConstraint .
TypeParamList = TypeParamDecl { "," TypeParamDecl } .
TypeParameters = "[" TypeParamList [ "," ] "]" .
TypeSpec = AliasDecl | TypeDef .
TypeSwitchCase = "case" TypeList | "default" .
TypeSwitchGuard = [ identifier ":=" ] PrimaryExpr "." "(" "type" ")" .
TypeSwitchStmt = "switch" [ SimpleStmt ";" ] TypeSwitchGuard "{" { TypeCaseClause } "}" .
TypeTerm = Type | UnderlyingType .
UnaryExpr         = PrimaryExpr         | ( "+" | "-" | "!" | "^" | "*" | "&" | "<-" ) UnaryExpr .
UnaryExprPreBlock = PrimaryExprPreBlock | ( "+" | "-" | "!" | "^" | "*" | "&" | "<-" ) UnaryExprPreBlock .
UnderlyingType = "~" Type .
VarDecl = "var" ( VarSpec | "(" { VarSpec ";" } ")" ) .
VarSpec = IdentifierList ( Type [ "=" ExpressionList ] | "=" ExpressionList ) .
float_lit = .
identifier = .
imaginary_lit = .
int_lit = .
rune_lit = .
string_lit = .
